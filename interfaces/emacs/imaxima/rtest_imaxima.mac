/* -*- mode: maxima -*- */
/* Regression tests for imaxima.lisp */
/*
*
* Author: Leo Butler (leo.butler@umanitoba.ca)
* Credit: These tests are adapted from the Maxima manual
*
* This file is Maxima code (http://maxima.sourceforge.net/)
*
* It is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 3 of the License, or (at your
  * option) any later version.
*
* This software is distributed in the hope that it will be useful, but
* WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
* or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public
* License for more details.
*
* You should have received a copy of the GNU General Public License
* along with this file. If not, see http://www.gnu.org/licenses/.
*/

/*
Usage:
batch("rtest_imaxima.mac",'test);
*/

wximplicit_plot (x^2 = y^3 - 3*y + 1, [x, -4, 4], [y, -4, 4]);
'implicit_plot;

wxplot2d (sin(x), [x, -%pi, %pi])$
plot2d$

wxplot2d (sec(x), [x, -2, 2], [y, -20, 20])$
plot2d$

wxplot2d ( x^2 - 1, [x, -3, 3], [box, false], grid2d,
                [yx_ratio, 1], [axes, solid], [xtics, -2, 4, 2],
                [ytics, 2, 2, 6], [label, ["x", 2.9, -0.3],
                ["x^2-1", 0.1, 8]], [title, "A parabola"])$
plot2d$

wxplot2d (exp(3*s), [s, -2, 2], logy)$
plot2d$

block([F,G,H],
  local(F,G,H),
  F(x) := x^2,
  G(x) := x^3,
  H(x) := if x < 0 then x^4 - 1 else 1 - x^5,
  wxplot2d ([F, G, H], [u, -1, 1], [y, -1.5, 1.5]))$
plot2d$

block([r],
  r: (exp(cos(t))-2*cos(4*t)-sin(t/12)^5),
  wxplot2d([parametric, r*sin(t), r*cos(t), [t, -8*%pi, 8*%pi]]))$
plot2d$

wxplot2d([[parametric, cos(t), sin(t), [t,0,2*%pi]], -abs(x)],
                   [x, -sqrt(2), sqrt(2)], same_xy)$
plot2d$

wxplot2d ([discrete, makelist ( random(10), 200)])$
plot2d$

wxplot2d ([discrete, makelist(i*%pi, i, 1, 5),
  [0.6, 0.9, 0.2, 1.3, 1]])$
plot2d$

block([data],
  data : genmatrix(lambda([x,i], x^i),10,3),
  wxplot2d ([discrete, transpose(data)[2], transpose(data)[3]],
    [style,points], [point_type,diamond], [color,red]))$
plot2d$

block([xy],
  xy: [[10, .6], [20, .9], [30, 1.1], [40, 1.3], [50, 1.4]],
  wxplot2d([[discrete, xy], 2*%pi*sqrt(l/980)], [l,0,50],
    [style, points, lines], [color, red, blue],
    [point_type, asterisk],
    [legend, "experiment", "theory"],
    [xlabel, "pendulum's length (cm)"],
    [ylabel, "period (s)"]))$
plot2d$

wxmandelbrot ([iterations, 30], [x, -2, 1], [y, -1.2, 1.2],
                      [grid,400,400])$
mandelbrot $

wxjulia (-0.55, 0.6, [iterations, 36], [x, -0.3, 0.2],
  [y, 0.3, 0.9], [grid, 400, 400], [color_bar_tics, 0, 6, 36])$
julia $

listp(wxdraw2d(color=red,  explicit(x^2,x,-1,1))) $
true $

listp(wxdraw( gr2d( key="sin (x)",grid=[2,2], explicit( sin(x), x,0,2*%pi ) ), gr2d( key="cos (x)",grid=[2,2], explicit( cos(x), x,0,2*%pi ) ) )) $
true $

/* end of rtest_imaxima.mac */
